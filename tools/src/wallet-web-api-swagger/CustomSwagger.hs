{-# LANGUAGE ScopedTypeVariables #-}
{-# LANGUAGE TypeOperators       #-}

-- | Allow to build swagger docs for api automatically

module CustomSwagger
    ( HasCustomSwagger (..)
    , toCustomSwagger
    ) where

import           Universum

import           Data.Swagger    (Swagger)
import           Servant         ((:<|>))
import           Servant.Swagger (HasSwagger (..))

-- | Also to provide some extra info about endpoints, in addition to info
-- generated by servant-swagger library.
class HasCustomSwagger api where
    docModifier :: Proxy api -> Swagger -> Swagger

instance (HasCustomSwagger a, HasCustomSwagger b) => HasCustomSwagger (a :<|> b) where
    docModifier _ = docModifier (Proxy @a) . docModifier (Proxy @b)

toCustomSwagger :: (HasSwagger api, HasCustomSwagger api) => Proxy api -> Swagger
toCustomSwagger = flip docModifier =<< toSwagger
